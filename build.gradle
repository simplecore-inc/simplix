plugins {
    id 'org.springframework.boot' version '3.5.7' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'java'
    id 'maven-publish'
}

ext {
    // ============================================================
    // Project Coordinates
    // ============================================================
    projectGroup = 'dev.simplecore.simplix'
    projectVersion = '1.0.16-SNAPSHOT'

    // ============================================================
    // Spring Framework
    // ============================================================
    springBootVersion = '3.5.7'
    springDocVersion = '2.7.0'

    // ============================================================
    // Database & ORM
    // ============================================================
    mybatisSpringBootVersion = '3.0.4'
    hypersistenceVersion = '3.9.4'

    // ============================================================
    // Searchable JPA (Legacy javax dependencies)
    // ============================================================
    searchableJpaVersion = '1.0.5'
    hibernateValidatorLegacyVersion = '6.2.5.Final'
    javaxPersistenceVersion = '2.2'
    javaxValidationVersion = '2.0.1.Final'
    javaxAnnotationVersion = '1.3.2'

    // ============================================================
    // Excel & Document Processing
    // ============================================================
    poiVersion = '5.2.4'
    jxlsVersion = '2.14.0'
    jxlsJexcelVersion = '1.0.9'
    commonsCsvVersion = '1.10.0'

    // ============================================================
    // Event & Messaging
    // ============================================================
    jnatsVersion = '2.16.14'

    // ============================================================
    // Utilities
    // ============================================================
    modelMapperVersion = '3.2.0'
    guavaVersion = '31.1-jre'
    jsr305Version = '3.0.2'
    uuidCreatorVersion = '6.1.1'
    javaparserVersion = '3.25.4'

    // ============================================================
    // Testing
    // ============================================================
    datafakerVersion = '2.0.2'

    // ============================================================
    // API Documentation
    // ============================================================
    scalarVersion = '0.3.11'

    // ============================================================
    // File Processing
    // ============================================================
    webpImageIoVersion = '0.8.0'

    // ============================================================
    // Email Providers
    // ============================================================
    awsSdkVersion = '2.29.26'
    sendGridVersion = '4.10.3'
    resendVersion = '3.1.0'
    greenMailVersion = '2.0.1'
}

allprojects {
    group = projectGroup
    version = projectVersion

    System.setProperty("spring.boot.version.check.enabled", "false")
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }

        dependencies {
            // SpringDoc OpenAPI (not managed by Spring Boot BOM)
            dependency "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
            dependency "org.springdoc:springdoc-openapi-starter-webmvc-api:${springDocVersion}"

            // Scalar API Reference (https://github.com/scalar/scalar)
            dependency "com.scalar.maven:scalar:${scalarVersion}"

            // Common Libraries (not managed by Spring Boot BOM)
            dependency "org.modelmapper:modelmapper:${modelMapperVersion}"

            // Database & Migration (not managed by Spring Boot BOM)
            dependency "io.hypersistence:hypersistence-utils-hibernate-60:${hypersistenceVersion}"

            // Data & Testing (not managed by Spring Boot BOM)
            dependency "net.datafaker:datafaker:${datafakerVersion}"

            // Excel & Document Processing (POI managed by Spring Boot BOM, others not)
            dependency "org.apache.poi:poi:${poiVersion}"
            dependency "org.apache.poi:poi-ooxml:${poiVersion}"
            dependency "org.jxls:jxls:${jxlsVersion}"
            dependency "org.jxls:jxls-poi:${jxlsVersion}"
            dependency "org.jxls:jxls-jexcel:${jxlsJexcelVersion}"
            dependency "org.apache.commons:commons-csv:${commonsCsvVersion}"

            // Java Parser (not managed by Spring Boot BOM)
            dependency "com.github.javaparser:javaparser-core:${javaparserVersion}"
            dependency "com.github.javaparser:javaparser-symbol-solver-core:${javaparserVersion}"

            // Event & Integration (not managed by Spring Boot BOM)
            dependency "io.nats:jnats:${jnatsVersion}"

            // Auth & Security (not managed by Spring Boot BOM)
            dependency "com.google.guava:guava:${guavaVersion}"
            dependency "com.google.code.findbugs:jsr305:${jsr305Version}"

            // UUID (not managed by Spring Boot BOM)
            dependency "com.github.f4b6a3:uuid-creator:${uuidCreatorVersion}"

            // MyBatis (not managed by Spring Boot BOM)
            dependency "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatisSpringBootVersion}"

            // Searchable JPA Dependencies (some managed by Spring Boot BOM)
            dependency "org.hibernate.validator:hibernate-validator:${hibernateValidatorLegacyVersion}"
            dependency "javax.persistence:javax.persistence-api:${javaxPersistenceVersion}"
            dependency "javax.validation:validation-api:${javaxValidationVersion}"
            dependency "javax.annotation:javax.annotation-api:${javaxAnnotationVersion}"
            dependency "dev.simplecore.searchable:spring-boot-starter-searchable-jpa:${searchableJpaVersion}"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                pom {
                    def metadata = [
                        'simplix-core': [
                            name: 'SimpliX Core',
                            description: 'Core library providing SPI patterns, base entities, tree repository support, and security utilities'
                        ],
                        'spring-boot-starter-simplix': [
                            name: 'Spring Boot Starter for SimpliX',
                            description: 'All-in-one umbrella starter that includes all SimpliX modules (auth, event, cache, hibernate, mybatis, excel, encryption)'
                        ],
                        'simplix-auth': [
                            name: 'SimpliX Auth',
                            description: 'Authentication and authorization with JWT/JWE token support and Spring Security integration'
                        ],
                        'simplix-event': [
                            name: 'SimpliX Event',
                            description: 'Event-driven architecture with strategy pattern supporting Local, Redis Pub/Sub, Kafka, and RabbitMQ'
                        ],
                        'simplix-hibernate': [
                            name: 'SimpliX Hibernate Cache',
                            description: 'Automatic Hibernate 2nd level cache management with support for Ehcache, Redis, Hazelcast, and Infinispan'
                        ],
                        'simplix-cache': [
                            name: 'SimpliX Cache',
                            description: 'SPI-based cache abstraction providing unified API for Caffeine (local) and Redis (distributed) caching'
                        ],
                        'simplix-mybatis': [
                            name: 'SimpliX MyBatis',
                            description: 'MyBatis integration with custom type handlers and mapper configurations'
                        ],
                        'simplix-excel': [
                            name: 'SimpliX Excel',
                            description: 'Excel and CSV import/export with Apache POI integration and template processing'
                        ],
                        'simplix-encryption': [
                            name: 'SimpliX Encryption',
                            description: 'Multi-provider encryption supporting Simple (dev), Managed (file-based), and Vault (production) key providers with rotation'
                        ],
                        'simplix-file': [
                            name: 'SimpliX File',
                            description: 'File upload, storage, and image processing with support for local filesystem and cloud storage'
                        ],
                        'simplix-email': [
                            name: 'SimpliX Email',
                            description: 'Multi-provider email service supporting SMTP, AWS SES, SendGrid, and Resend with template support'
                        ],
                        'simplix-scheduler': [
                            name: 'SimpliX Scheduler',
                            description: 'Scheduler execution logging with support for @Scheduled methods, ShedLock integration, and multiple storage strategies'
                        ]
                    ]
                    if (metadata.containsKey(project.name)) {
                        def projectMetadata = metadata[project.name]
                        setName(projectMetadata['name'])
                        setDescription(projectMetadata['description'])
                    } else {
                        throw new RuntimeException("No metadata found for project: ${project.name}")
                    }
                    url = 'https://github.com/simplecore-inc/simplix'

                    licenses {
                        license {
                            name = 'SimpliX License, Version 1.0'
                            url = 'https://github.com/simplecore-inc/simplix/LICENSE.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'simplecore'
                            name = 'SimpleCORE'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/simplecore-inc/simplix.git'
                        developerConnection = 'scm:git:ssh://github.com:simplecore-inc/simplix.git'
                        url = 'https://github.com/simplecore-inc/simplix.git'
                    }
                }
            }
        }

        repositories {
            mavenLocal()
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/simplecore-inc/simplix")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        withSourcesJar()
        withJavadocJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs.addAll(['-parameters', "-Xlint:-deprecation", "-Xlint:-options"])
    }

    tasks.withType(Javadoc).configureEach {
        options.encoding = 'UTF-8'
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('Xmaxwarns', '1')
    }

    test {
        useJUnitPlatform()
        jvmArgs '-Xshare:off'
    }
}

wrapper {
    gradleVersion = '8.5'
    distributionType = Wrapper.DistributionType.BIN
}