plugins {
    id 'org.springframework.boot' version '3.5.5' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'java'
    id 'maven-publish'
}

ext {
    springBootVersion = '3.5.5'
    springDocVersion = '2.7.0'
    
    // Common Libraries (not managed by Spring Boot BOM)
    modelMapperVersion = '3.2.0'
    
    // Database & Migration (not managed by Spring Boot BOM)
    hypersistenceVersion = '3.9.4'
    
    // Data & Testing (not managed by Spring Boot BOM)
    datafakerVersion = '2.0.2'
    
    // Excel & Document Processing (not managed by Spring Boot BOM)
    poiVersion = '5.2.4'
    jxlsVersion = '2.14.0'
    jxlsJexcelVersion = '1.0.9'
    commonsCsvVersion = '1.10.0'
    
    // Java Parser (not managed by Spring Boot BOM)
    javaparserVersion = '3.25.4'
    
    // Event & Integration (not managed by Spring Boot BOM)
    jnatsVersion = '2.16.14'
    
    // Auth & Security (not managed by Spring Boot BOM)
    guavaVersion = '31.1-jre'
    jsr305Version = '3.0.2'
    
    // UUID (not managed by Spring Boot BOM)
    uuidCreatorVersion = '6.1.1'
    
    // MyBatis (not managed by Spring Boot BOM)
    mybatisSpringBootVersion = '3.0.4'
    
    // Searchable JPA Dependencies (some managed by Spring Boot BOM)
    hibernateValidatorLegacyVersion = '6.2.5.Final'
    javaxPersistenceVersion = '2.2'
    javaxValidationVersion = '2.0.1.Final'
    javaxAnnotationVersion = '1.3.2'
    searchableJpaVersion = '1.0.0-SNAPSHOT'
}

allprojects {
    group = 'dev.simplecore.simplix'
    version = '1.0.0-SNAPSHOT'
    
    System.setProperty("spring.boot.version.check.enabled", "false")
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'
    
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
        
        dependencies {
            // SpringDoc OpenAPI (not managed by Spring Boot BOM)
            dependency "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
            dependency "org.springdoc:springdoc-openapi-starter-webmvc-api:${springDocVersion}"
            
            // Common Libraries (not managed by Spring Boot BOM)
            dependency "org.modelmapper:modelmapper:${modelMapperVersion}"
            
            // Database & Migration (not managed by Spring Boot BOM)
            dependency "io.hypersistence:hypersistence-utils-hibernate-60:${hypersistenceVersion}"
            
            // Data & Testing (not managed by Spring Boot BOM)
            dependency "net.datafaker:datafaker:${datafakerVersion}"
            
            // Excel & Document Processing (POI managed by Spring Boot BOM, others not)
            dependency "org.apache.poi:poi:${poiVersion}"
            dependency "org.apache.poi:poi-ooxml:${poiVersion}"
            dependency "org.jxls:jxls:${jxlsVersion}"
            dependency "org.jxls:jxls-poi:${jxlsVersion}"
            dependency "org.jxls:jxls-jexcel:${jxlsJexcelVersion}"
            dependency "org.apache.commons:commons-csv:${commonsCsvVersion}"
            
            // Java Parser (not managed by Spring Boot BOM)
            dependency "com.github.javaparser:javaparser-core:${javaparserVersion}"
            dependency "com.github.javaparser:javaparser-symbol-solver-core:${javaparserVersion}"
            
            // Event & Integration (not managed by Spring Boot BOM)
            dependency "io.nats:jnats:${jnatsVersion}"
            
            // Auth & Security (not managed by Spring Boot BOM)
            dependency "com.google.guava:guava:${guavaVersion}"
            dependency "com.google.code.findbugs:jsr305:${jsr305Version}"
            
            // UUID (not managed by Spring Boot BOM)
            dependency "com.github.f4b6a3:uuid-creator:${uuidCreatorVersion}"
            
            // MyBatis (not managed by Spring Boot BOM)
            dependency "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatisSpringBootVersion}"
            
            // Searchable JPA Dependencies (some managed by Spring Boot BOM)
            dependency "org.hibernate.validator:hibernate-validator:${hibernateValidatorLegacyVersion}"
            dependency "javax.persistence:javax.persistence-api:${javaxPersistenceVersion}"
            dependency "javax.validation:validation-api:${javaxValidationVersion}"
            dependency "javax.annotation:javax.annotation-api:${javaxAnnotationVersion}"
            dependency "dev.simplecore.searchable:spring-boot-starter-searchable-jpa:${searchableJpaVersion}"
        }
    }
    
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                pom {
                    def metadata = [
                        'simplix-core': [
                            name: 'SimpliX Core',
                            description: 'Core library for SimpliX Framework'
                        ],
                        'spring-boot-starter-simplix': [
                                name: 'Spring Boot Starter for SimpliX',
                                description: 'Spring Boot Starter for SimpliX Framework'
                        ],
                        'spring-boot-starter-simplix-auth': [
                            name: 'SimpliX Auth',
                            description: 'Auth library for SimpliX Framework'
                        ],
                        'spring-boot-starter-simplix-event': [
                            name: 'SimpliX Event',
                            description: 'Event library for SimpliX Framework'
                        ],
                        'spring-boot-starter-simplix-mybatis': [
                            name: 'Spring Boot Starter for SimpliX MyBatis',
                            description: 'Spring Boot Starter for SimpliX MyBatis'
                        ],
                        'spring-boot-starter-simplix-excel': [
                            name: 'Spring Boot Starter for SimpliX Excel',
                            description: 'Spring Boot Starter for SimpliX Excel'
                        ]
                    ]
                    if (metadata.containsKey(project.name)) {
                        def projectMetadata = metadata[project.name]
                        setName(projectMetadata['name'])
                        setDescription(projectMetadata['description'])
                    } else {
                        throw new RuntimeException("No metadata found for project: ${project.name}")
                    }
                    url = 'https://github.com/simplecore-inc/simplix'
                    
                    licenses {
                        license {
                            name = 'SimpliX License, Version 1.0'
                            url = 'https://github.com/simplecore-inc/simplix/LICENSE.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'simplecore'
                            name = 'SimpleCORE'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/simplecore-inc/simplix.git'
                        developerConnection = 'scm:git:ssh://github.com:simplecore-inc/simplix.git'
                        url = 'https://github.com/simplecore-inc/simplix.git'
                    }
                }
            }
        }
        
        repositories {
            mavenLocal()
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/simplecore-inc/simplix")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs.addAll(['-parameters', "-Xlint:-deprecation", "-Xlint:-options"])
    }

    test {
        useJUnitPlatform()
    }
}

wrapper {
    gradleVersion = '8.5'
    distributionType = Wrapper.DistributionType.BIN
}