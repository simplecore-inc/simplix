import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

ext {
    set('springBootVersion', '3.5.5')
    set('nimbusJoseVersion', '9.37')
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

dependencies {
    api project(':simplix-core')

    // External Libraries
    api 'org.modelmapper:modelmapper'
    
    // Spring Boot Dependencies
    api 'org.springframework.boot:spring-boot-starter'
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-validation'
    api 'org.springframework.boot:spring-boot-starter-actuator'
    api 'org.springframework.boot:spring-boot-starter-json'
    api 'org.springframework.boot:spring-boot-starter-security'
    api 'org.springframework.boot:spring-boot-starter-thymeleaf'
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    
    // OpenAPI/Swagger
    api 'org.springdoc:springdoc-openapi-starter-webmvc-ui'
    api 'org.springdoc:springdoc-openapi-starter-webmvc-api'
    
    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    
    // Spring Boot Optional Dependencies
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    
    // Test Dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Additional Dependencies

    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'

    // Thymeleaf
    api 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'

    // Spring Session
    api 'org.springframework.session:spring-session-jdbc'

    // JWT/JWE Support
    api "com.nimbusds:nimbus-jose-jwt:${nimbusJoseVersion}"

    // Database
    runtimeOnly 'com.h2database:h2'

    implementation 'com.google.code.findbugs:jsr305'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

test {
    useJUnitPlatform()
}

compileJava {
    options.compilerArgs << "-Xlint:deprecation"
}

compileJava.inputs.files(processResources)

processResources {
    def projectVersion = project.version
    def springBootVer = springBootVersion
    
    filesMatching(['**/banner.txt', '**/config/application-info.txt']) {
        filter(ReplaceTokens, tokens: [
            'simplix.version': projectVersion,
            'spring-boot.version': springBootVer
        ])
    }
}
