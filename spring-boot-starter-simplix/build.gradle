apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext {
    set('springBootVersion', '2.7.18')
    set('nimbusJoseVersion', '9.31')
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

dependencies {
    api project(':simplix-core')

    // External Libraries
    api 'com.github.thkwag.searchable:spring-boot-starter-searchable-jpa:0.0.2'
    api 'org.modelmapper:modelmapper:3.2.0'
    
    // Spring Boot Dependencies
    api 'org.springframework.boot:spring-boot-starter'
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-validation'
    api 'org.springframework.boot:spring-boot-starter-actuator'
    api 'org.springframework.boot:spring-boot-starter-security'
    api 'org.springframework.boot:spring-boot-starter-thymeleaf'
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    
    // OpenAPI/Swagger
    api 'org.springdoc:springdoc-openapi-ui:1.7.0'
    api 'org.springdoc:springdoc-openapi-webmvc-core:1.7.0'
    api 'org.springdoc:springdoc-openapi-javadoc:1.7.0'
    api 'org.springdoc:springdoc-openapi-data-rest:1.7.0'
    
    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    
    // Spring Boot Optional Dependencies
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    
    // Test Dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Additional Dependencies
    api 'javax.persistence:javax.persistence-api'
    api 'javax.validation:validation-api'
    api 'com.github.thkwag.searchable:searchable-jpa-core:0.0.2'
    api 'com.github.thkwag.searchable:searchable-jpa-openapi:0.0.2'

    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'

    // Thymeleaf
    api 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
    api 'org.thymeleaf.extras:thymeleaf-extras-java8time'

    // Spring Session
    api 'org.springframework.session:spring-session-jdbc'

    // JWT/JWE Support
    api "com.nimbusds:nimbus-jose-jwt:${nimbusJoseVersion}"

    // Database
    runtimeOnly 'com.h2database:h2'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

test {
    useJUnitPlatform()
}

compileJava.inputs.files(processResources)

processResources {
    def projectVersion = project.version
    def springBootVer = springBootVersion
    
    filesMatching(['**/banner.txt', '**/config/application-info.txt']) {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            'simplix.version': projectVersion,
            'spring-boot.version': springBootVer
        ])
    }
}
